*********************************************************************
*                                                                   *
*  TMLIBA02 - PROGRAM TO CREATE A LIST OF VOLUMES IN COLUMNS 1-6    *
*             WHICH IS MANUFACTURED FROM THE OUTPUT OF A TSO        *
*             LISTCAT COMMAND AGAINST THE SYSCAT.VOLCAT.VGENERAL    *
*             DATASET.  THIS IS THE TCDB DATASET, AND IT IS MUCH    *
*             LIKE A CATALOG.                                       *
*                                                                   *
*        LISTC ENT('SYSCAT.VOLCAT.VGENERAL') VOLENTRIES(*)          *
*                                                                   *
*     AUTHOR:   SAM GOLOB            DATE:  05/31/01                *
*                                                                   *
*     INPUTS:   VOLCIN   -  THE SYSTSPRT OUTPUT OF THE LISTCAT      *
*                           COMMAND - EXPECTED TO BE FB-133         *
*                                                                   *
*     OUTPUTS:  VOLLIST  -  OUTPUT FB-80 LIST OF VOLUMES            *
*               SYSPRINT -  PRINTS RUN STATISTICS                   *
*                                                                   *
*     CHANGE LOG:                                                   *
*                                                                   *
*                                                                   *
*                                                                   *
*********************************************************************
TMLIBA02 CSECT
         YREGS
         USING *,R15
         B     EYECATCH
         DC    C'--- '            MAKE EYECATCHER VISIBLE
         DC    C'TMLIBA02 - CREATE VOL LIST FROM LISTC OF VOLCAT '
         DC    C'&SYSDATE',C'  ',C'&SYSTIME ',C'--- '
         DC    C'LEVEL 001 '
         DC    C'--- '            MAKE EYECATCHER VISIBLE
SAVEAREA DC    18F'0'
EYECATCH DS    0H             ENSURE ALIGNMENT
         SAVE  (14,12)
         LR    R5,R1          SAVE PARM POINTER
         LA    R1,SAVEAREA          NEW SAVE-AREA ADDR
         ST    R1,8(,R13)           FORWARD SAVE-AREA CHAIN PTR
         ST    R13,4(,R1)           BACKWARD SAVE-AREA CHAIN PTR
         LR    R13,R1               NEW SAVE AREA ADDRESS
         DROP  R15
* -----                       SET UP TWO BASE REGISTERS
         USING TMLIBA02,R12,R11
         LR    R12,R15
         LR    R11,R12
         LA    R11,4095(,R12)
         LA    R11,1(,R11)
         LR    R1,R5          RESTORE PARM POINTER
         PRINT GEN
***********************************************************************
* GET SYSTEM DATE AND TIME                                            *
***********************************************************************
         TIME  DEC,DATETIME,LINKAGE=SYSTEM,DATETYPE=MMDDYYYY
         OI    DATE+4,X'0F'
         UNPK  DATED,DATE
         OI    TIME+3,X'0F'
         UNPK  TIMED,TIME
         MVC   HEADDATE(2),DATED
         MVI   HEADDATE+2,C'/'
         MVC   HEADDATE+3(2),DATED+2
         MVI   HEADDATE+5,C'/'
         MVC   HEADDATE+6(4),DATED+4
         MVC   HEADDATE+12(2),TIMED
         MVI   HEADDATE+14,C':'
         MVC   HEADDATE+15(2),TIMED+2
         MVI   HEADDATE+17,C':'
         MVC   HEADDATE+18(2),TIMED+4
         MVC   HEADER+95(23),HEADDATE
***********************************************************************
         OPEN  (VOLCIN,(INPUT))
         OPEN  (VOLLIST,(OUTPUT))
         ZAP   INRCDS,=P'0'
         ZAP   OUTRCDS,=P'0'
LEER     GET   VOLCIN,AREAIN
         AP    INRCDS,=P'1'
         CLC   AREAIN+12(5),=C'----V'     CONTAINS VOLSER?
         BNE   LEER                       IGNORE IT
         MVC   AREAOUT,AREAOUT-1         CLEAR OUTPUT AREA
         MVC   AREAOVOL(6),AREAIN+17      MOVE VOLUME SERIAL
         MVC   AREAOUT+30(VOLCINDL),VOLCIND  INDICATE VOL FROM VOLCAT
         PUT   VOLLIST,AREAOUT
         AP    OUTRCDS,=P'1'
         B     LEER
FIN      DS    0H
         OPEN  (SYSPRINT,OUTPUT)
         MVC   OUTLINE,OUTLINE-1
         MVC   OUTLINE(133),HEADER
         PUT   SYSPRINT,OUTLINE
         MVC   OUTLINE,OUTLINE-1
         MVC   OUTLINE(133),MSG01
         MVC   OUTLINE+19(12),EDPAT
         ED    OUTLINE+19(12),INRCDS+2
         PUT   SYSPRINT,OUTLINE
         MVC   OUTLINE,OUTLINE-1
         MVC   OUTLINE(133),MSG02
         MVC   OUTLINE+19(12),EDPAT
         ED    OUTLINE+19(12),OUTRCDS+2
         PUT   SYSPRINT,OUTLINE
         CLOSE VOLCIN
         CLOSE VOLLIST
         CLOSE SYSPRINT
THEEND   DS    0H
         L     R13,4(,R13)
         RETURN (14,12),RC=0
* ----- CONSTANTS ------ *
ONE      DC    F'1'
DECVALUE DS    CL8
EDPAT    DC    X'402020206B2020206B202120'
         DC    C' '
BLANKS   DC    CL133' '
OUTLINE  DS    CL133
HEADER   DC    CL133'1  TMLIBA02 - VOLCAT VOLUME NAME EXTRACT PROGRAM'
         DC    CL133' '
MSG01    DC    CL133'-    RECORDS  IN ='
         DC    CL133' '
MSG02    DC    CL133'     RECORDS OUT ='
         DC    CL133' '
VOLCIND  DC    C'FROM THE VOLCAT'
VOLCINDL EQU   *-VOLCIND
INRCDS   DC    PL7'0'
OUTRCDS  DC    PL7'0'
         LTORG
VOLCIN   DCB   DDNAME=VOLCIN,MACRF=GM,EODAD=FIN,DSORG=PS,LRECL=133
VOLLIST  DCB   DDNAME=VOLLIST,MACRF=PM,DSORG=PS,LRECL=80
SYSPRINT DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=PM,LRECL=133,RECFM=FBA
         DC    CL8' '
AREAIN   DS    0CL133
AREAIVOL DS    CL6
AREAIRST DS    CL127
         DC    CL8' '
AREAOUT  DS    0CL80
AREAOVOL DS    CL6
         DC    CL74' '
         DC    CL8' '
TIMED    DS    CL7
DATED    DS    CL9
DATETIME DS    4F
         ORG   DATETIME
TIME     DS    CL4
         DS    CL4
DATE     DS    CL5
         DS    CL3
HEADDATE DC    CL23' '
         END   TMLIBA02
