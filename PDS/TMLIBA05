*********************************************************************
*                                                                   *
*  TMLIBA05 - PROGRAM TO CREATE COMPATIBLE OUTPUT TO TMLIBA04,      *
*             WITHOUT HAVING TO QUERY THE LIBRARY MANAGER.  THIS    *
*             PROGRAM ONLY TAKES INPUT FROM THE VOLCAT.  INPUT IS   *
*             LISTCAT COMMAND AGAINST THE SYSCAT.VOLCAT.VGENERAL    *
*             DATASET.  THIS IS THE TCDB DATASET, AND IT IS MUCH    *
*             LIKE A CATALOG.                                       *
*                                                                   *
*        LISTC ENT('SYSCAT.VOLCAT.VGENERAL') VOLENTRIES(*) ALL      *
*                                                                   *
*     AUTHOR:   SAM GOLOB            DATE:  06/07/01                *
*                                                                   *
*     INPUTS:   VOLCIN   -  THE SYSTSPRT OUTPUT OF THE LISTCAT      *
*                           COMMAND - EXPECTED TO BE FB-133         *
*                                                                   *
*     OUTPUTS:  VOLLIST  -  OUTPUT FB-385 FILE, SOMEWHAT COMPATIBLE *
*                           WITH THE OUTPUT OF TMLIBA04.            *
*                                                                   *
*               SYSPRINT -  PRINTS RUN STATISTICS                   *
*                                                                   *
*     CHANGE LOG:                                                   *
*                                                                   *
*                                                                   *
*                                                                   *
*********************************************************************
TMLIBA05 CSECT
         YREGS
         USING *,R15
         B     EYECATCH
         DC    C'--- '            MAKE EYECATCHER VISIBLE
         DC    C'TMLIBA05 - CREATE VOL LIST FROM LISTC OF VOLCAT '
         DC    C'&SYSDATE',C'  ',C'&SYSTIME ',C'--- '
         DC    C'LEVEL 002 '
         DC    C'--- '            MAKE EYECATCHER VISIBLE
SAVEAREA DC    18F'0'
EYECATCH DS    0H             ENSURE ALIGNMENT
         SAVE  (14,12)
         LR    R5,R1          SAVE PARM POINTER
         LA    R1,SAVEAREA          NEW SAVE-AREA ADDR
         ST    R1,8(,R13)           FORWARD SAVE-AREA CHAIN PTR
         ST    R13,4(,R1)           BACKWARD SAVE-AREA CHAIN PTR
         LR    R13,R1               NEW SAVE AREA ADDRESS
         DROP  R15
* -----                       SET UP TWO BASE REGISTERS
         USING TMLIBA05,R12,R11
         LR    R12,R15
         LR    R11,R12
         LA    R11,4095(,R12)
         LA    R11,1(,R11)
         LR    R1,R5          RESTORE PARM POINTER
         PRINT GEN
***********************************************************************
* GET SYSTEM DATE AND TIME                                            *
***********************************************************************
         TIME  DEC,DATETIME,LINKAGE=SYSTEM,DATETYPE=MMDDYYYY
         OI    DATE+4,X'0F'
         UNPK  DATED,DATE
         OI    TIME+3,X'0F'
         UNPK  TIMED,TIME
         MVC   HEADDATE(2),DATED
         MVI   HEADDATE+2,C'/'
         MVC   HEADDATE+3(2),DATED+2
         MVI   HEADDATE+5,C'/'
         MVC   HEADDATE+6(4),DATED+4
         MVC   HEADDATE+12(2),TIMED
         MVI   HEADDATE+14,C':'
         MVC   HEADDATE+15(2),TIMED+2
         MVI   HEADDATE+17,C':'
         MVC   HEADDATE+18(2),TIMED+4
         MVC   HEADER+95(23),HEADDATE
***********************************************************************
         OPEN  (VOLCIN,(INPUT))
         OPEN  (VOLLIST,(OUTPUT))
         ZAP   INRCDS,=P'0'
         ZAP   OUTRCDS,=P'0'
GETINP   DS    0H
         MVI   VFLAG,X'00'                   CLEAR FLAGS
*                                        CLEAR OUTPUT AREA
         MVC   AREAOUT(256),AREAOUT-1
         MVC   AREAOUT+256(AREAOUTL-256),AREAOUT+255
*
         BAL   R9,TESTVOL                GET LISTCAT INFO FOR VOLUME
*        MVC   AREAOUT+15(VOLCINDL),VOLCIND  INDICATE VOL FROM VOLCAT
         MVC   ERRFUNC+(5),=C'VCT  '
         MVC   AREAOUT+42(5),=C'VCT  '
         PUT   VOLLIST,AREAOUT
         AP    OUTRCDS,=P'1'
         B     GETINP
FIN      DS    0H
         OPEN  (SYSPRINT,OUTPUT)
         MVC   OUTLINE,OUTLINE-1
         MVC   OUTLINE(133),HEADER
         PUT   SYSPRINT,OUTLINE
         MVC   OUTLINE,OUTLINE-1
         MVC   OUTLINE(133),MSG01
         MVC   OUTLINE+19(12),EDPAT
         ED    OUTLINE+19(12),INRCDS+2
         PUT   SYSPRINT,OUTLINE
         MVC   OUTLINE,OUTLINE-1
         MVC   OUTLINE(133),MSG02
         MVC   OUTLINE+19(12),EDPAT
         ED    OUTLINE+19(12),OUTRCDS+2
         PUT   SYSPRINT,OUTLINE
         CLOSE VOLCIN
         CLOSE VOLLIST
         CLOSE SYSPRINT
THEEND   DS    0H
         L     R13,4(,R13)
         RETURN (14,12),RC=0
* -------------------------- SUBROUTINES --------------------------- *
TESTVOL  DS    0H
         GET   VOLCIN,AREAIN
         AP    INRCDS,=P'1'
         TM    VFLAG,X'01'               ONLY ONCE
         BO    TESTLIB                     PER VOLSER
         CLC   AREAIN(17),=C'VOLUME-ENTRY----V'   VOLSER?
         BNE   TESTVOL
         OI    VFLAG,X'01'               INDICATE VOLUME GOTTEN
         MVC   AREAOUT+20(6),AREAIN+17      MOVE VOLUME SERIAL
         MVC   AREAOUT+58(6),AREAIN+17      MOVE VOLUME SERIAL
         B     TESTVOL                   GET ANOTHER RECORD LINE
TESTLIB  DS    0H
         TM    VFLAG,X'02'               ONLY ONCE
         BO    TESTUSE                     PER VOLSER
         CLC   AREAIN+7(16),=C'LIBRARY---------'
         BNE   TESTVOL
         OI    VFLAG,X'02'               INDICATE LIBRARY GOTTEN
         MVC   AREAOUT+26(8),BLANKS      BLANK TARGET AREA
         LA    R6,AREAIN+23              PREPARE VARIABLE MOVE
         LA    R7,8                      8 CHARS MAX
TESTSTRT DS    0H
         CLI   0(R6),C'-'
         BNE   TESTMOVE
         LA    R6,1(,R6)                 START ONE CHAR LATER
         BCTR  R7,0                      MOVE ONE CHAR LESS
         B     TESTSTRT
TESTMOVE DS    0H
         BCTR  R7,0                      ONE LESS FOR EXECUTE
         EX    R7,TESTML
         B     TESTVOL                   GET ANOTHER RECORD LINE
TESTML   MVC   AREAOUT+26(*-*),0(R6)           EXECUTED
TESTUSE  DS    0H
         TM    VFLAG,X'04'               ONLY ONCE
         BO    TESTOWNR
         CLC   AREAIN+65(17),=C'USE-ATTRIBUTE----'
         BNE   TESTVOL
         OI    VFLAG,X'04'               INDICATE USAGE GOTTEN
         CLC   AREAIN+82(7),=C'PRIVATE'
         BNE   TESTSCR
         MVI   AREAOUT+64,C'P'           INDICATE PRIVATE VOLUME
         B     TESTVOL
TESTSCR  DS    0H
         CLC   AREAIN+82(7),=C'SCRATCH'
         BNE   TESTERR
         MVI   AREAOUT+64,C'S'           INDICATE SCRATCH VOLUME
         B     TESTVOL
TESTERR  DS    0H
         MVI   AREAOUT+64,C'E'           INDICATE ERROR
         B     TESTVOL
TESTOWNR DS    0H
         TM    VFLAG,X'08'               ONLY ONCE
         BO    TESTVOLE
         CLC   AREAIN+7(16),=C'OWNER-----------'
         BNE   TESTVOL
         OI    VFLAG,X'08'
TESTVOLE BR    R9
* ------------------------------------------------------------------ *
* ----- CONSTANTS ------ *
ONE      DC    F'1'
VFLAG    DC    X'00'
         DC    CL1' '
DECVALUE DS    CL8
EDPAT    DC    X'402020206B2020206B202120'
         DC    C' '
BLANKS   DC    CL133' '
OUTLINE  DS    CL133
HEADER   DC    CL133'1  TMLIBA05 - VOLCAT VOLUME NAME EXTRACT PROGRAM'
         DC    CL133' '
MSG01    DC    CL133'-    RECORDS  IN ='
         DC    CL133' '
MSG02    DC    CL133'     RECORDS OUT ='
         DC    CL133' '
VOLCIND  DC    C'FROM THE VOLCAT'
VOLCINDL EQU   *-VOLCIND
INRCDS   DC    PL7'0'
OUTRCDS  DC    PL7'0'
         LTORG
VOLCIN   DCB   DDNAME=VOLCIN,MACRF=GM,EODAD=FIN,DSORG=PS,LRECL=133
VOLLIST  DCB   DDNAME=VOLLIST,MACRF=PM,DSORG=PS,LRECL=385
SYSPRINT DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=PM,LRECL=133,RECFM=FBA
         DC    CL8' '
AREAIN   DS    0CL133
AREAIVOL DS    CL6
AREAIRST DS    CL127
*
         DC    CL8' '
AREAOUT  DS    0CL385
PRETCODE DC    D'0'                 CBRXLCS RETURN CODE - PACKED DEC
PRSNCODE DC    D'0'                 CBRXLCS REASON CODE - PACKED DEC
AREAOVOL DC    CL6' '
TVIOLIB  DC    CL8' '
TVIOCONS DC    CL8' '
TVIOAREA DC    288X'00'                288 CHARACTERS
         SPACE 2
RETREAS  DS    0CL45
ERRFUNC  DC    CL5' '               'QVR', 'CUA', OR 'EJECT'
         DC    CL15' RETURN CODE = '
ZRETCODE DC    CL4' '               CBRXLCS RETURN CODE - ZONED DEC
         DC    CL16', REASON CODE = '
ZRSNCODE DC    CL4' '               CBRXLCS REASON CODE - ZONED DEC
         DC    CL1'.'
         DC    CL14' '            FILL OUT 385 CHARACTER OUTPUT
AREAOUTL EQU   *-AREAOUT
         DC    CL8' '
TIMED    DS    CL7
DATED    DS    CL9
DATETIME DS    4F
         ORG   DATETIME
TIME     DS    CL4
         DS    CL4
DATE     DS    CL5
         DS    CL3
HEADDATE DC    CL23' '
         END   TMLIBA05
