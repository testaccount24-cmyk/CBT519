TMSUX2U  TITLE 'CA-1 TAPE MANAGEMENT SYSTEM  -  MODULE (TMSUX2U)'
*****************************************************************
*    TMSUX2U - USER EDIT ROUTINE FOR ALPHANUMERIC VOLSERS
*
*    REGISTER USAGE
*        R0    DO NOT DESTROY
*        R1    ADDRESS OF VOLSER FOLLOWED BY 10 BYTE WORK AREA
*        R2    DO NOT DESTROY
*        R3    DO NOT DESTROY
*        R4    DO NOT DESTROY
*        R5    DO NOT DESTROY
*        R6    DO NOT DESTROY
*        R7    DO NOT DESTROY
*        R8    DO NOT DESTROY
*        R9    DO NOT DESTROY
*        R10   DO NOT DESTROY
*        R11   DO NOT DESTROY
*        R12   DO NOT DESTROY
*        R13   CALLER'S SAVE AREA
*        R14   RETURN ADDRESS
*        R15   BASE REGISTER
*
*    SPECIFICATIONS
*        REENTRANT
*        AMODE ANY
*        RMODE 24
*
*    EXIT
*        VOLSER POINTED TO BY R1 IS ALL NUMERIC FOR CONTROLLED TAPE
*        AND NON-NUMERIC FOR NON-CA-1 CONTROLLED TAPE
*
*****************************************************************
*    MAINTENANCE LOG                                                  *
*    ADDED AMODE ANY AND RMODE 24 FOR PROPER LKED              TEAA084*
*    ADDED V AND DV TAPES  SBG 5/01                                   *
***********************************************************************
         EJECT
R0       EQU   0                  DO NOT DESTROY
R1       EQU   1                  ADDRESS OF VOLSER AND WORK AREA
R2       EQU   2                  DO NOT DESTROY
R3       EQU   3                  DO NOT DESTROY
R4       EQU   4                  DO NOT DESTROY
R5       EQU   5                  DO NOT DESTROY
R6       EQU   6                  DO NOT DESTROY
R7       EQU   7                  DO NOT DESTROY
R8       EQU   8                  DO NOT DESTROY
R9       EQU   9                  DO NOT DESTROY
R10      EQU   10                 DO NOT DESTROY
R11      EQU   11                 DO NOT DESTROY
R12      EQU   12                 DO NOT DESTROY
R13      EQU   13                 CALLER'S SAVEAREA
R14      EQU   14                 RETURN ADDRESS
R15      EQU   15                 BASE REGISTER
         EJECT
TMSUX2U  CSECT ,
TMSUX2U  AMODE ANY                                              TEAA084
TMSUX2U  RMODE 24                                               TEAA084
         USING TMSUX2U,R15
         TMMLINK COPY=ONLY,                                            X
               SSI=CL052B0                                     CL052B0
         SPACE 2
         CLC   0(2,R1),=C'DV'     NEW DEVELOPMENT VOLUMES?          SBG
         BNE   DOTRAN             NO. REGULAR PROCESSING            SBG
         MVC   0(2,R1),=C'10'     YES. MOVE IN '10' INSTEAD         SBG
         B     RETURN             AND GET OUT                       SBG
*
DOTRAN   DS    0H                                                   SBG
         TR    0(1,1),EXT2INT     CONVERT EXTERNAL (IBM) VOLSER    !HS1
*                                  TO INTERNAL (CA1) REPRESENTATION!HS1
*
RETURN   DS    0H                                                   SBG
         BR    R14                RETURN TO CALLER
         EJECT
*------------------------------------------------------------------!HS1
*                                                                  !HS1
* THE PURPOSE OF THIS ROUTINE IS TO CONVERT EXTERNAL VOLUME SERIAL !HS1
* NUMBERS TO INTERNAL UCC-1 FORMAT. UCC-1 REQUIRES ALL NUMERIC     !HS1
* VOL SERS FOR ITS PROCESSING. SINCE A & P USES THE FIRST CHARATER !HS1
* OF OUR VOL SERS TO INDICATE THE "OWNER" OF THE TAPE, AN ALPHA TO !HS1
* TO NUMERIC MAPPING IS REQUIRED. IN ADDITION, ANY TAPE WITH A     !HS1
* NUMERIC FIRST LETTER IS MADE ALPHA, SO THAT UCC-1 WILL TREAT IT  !HS1
* AS THE FOREIGN TAPE IT IS.                                       !HS1
*                                                                  !HS1
*------------------------------------------------------------------!HS1
*   ANP TABLE FOR TRANSLATION FROM EXTERNAL (CA1) VOLSER TO        !HS1
*   INTERNAL (CA1) REPRESENTATION                                  !HS1
*------------------------------------------------------------------!HS1
EXT2INT  DS    0D                                                  !HS1
         DC    256AL1(*-EXT2INT) TABLE THAT TRANSLATES TO ITSELF   !HS1
         ORG   EXT2INT+C'0'                                        !HS1
         DC    C'ABCDEFGHIJ'     0->A,....9->J                     !HS1
*                                                                  !HS1
         ORG   EXT2INT+C'D'     D FOR DEVELOPMENT -> 0             !HS1
         DC    C'0'                                                !HS1
*        ORG   EXT2INT+C'DV'    DV FOR DEV VIRT   -> 1  NON-FUNCTIONAL
*        DC    C'1'                                     DOC ONLY
         ORG   EXT2INT+C'C'     C FOR COLUMBIA    -> 2             !HS1
         DC    C'2'                                                !HS1
         ORG   EXT2INT+C'M'     M FOR MONTVALE    -> 4             !HS1
         DC    C'4'                                                !HS1
         ORG   EXT2INT+C'T'     T FOR TORONTO     -> 6             !HS1
         DC    C'6'                                                !HS1
         ORG   EXT2INT+C'V'     V FOR VIRT TAPES  -> 8             !HS1
         DC    C'8'                                                !HS1
         ORG   EXT2INT+C'W'     W FOR TECH SVCS   -> 9             !HS1
         DC    C'9'                                                !HS1
         ORG
         LTORG ,
         TMMLINK PATCH,PSIZE=16
         END
