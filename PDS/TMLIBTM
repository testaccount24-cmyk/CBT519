!
!  TMS REPORT-TMLIBTM   VOLUME SERIAL MASTER
!     ( SIMILAR TO OLD TMS REPORT 1 )
!       VOLUME AND/OR RECORDS ONLY
!
!
!-----------------------  MAINTENANCE LOG  ----------------------------
!
! ADD TOTALS TO REPORTS AND GENERAL ENHANCEMENTS                TE9A022
!
 COPY EARLDEFS

 DEF D_LBLTYPE         (X 4) = NONE 'LBL' 'TYPE'
 DEF D_VOLSER          (X 6) = NONE 'VOLUME' 'SERIAL'
 DEF D_CTIME           (P 4.0) = NONE 'CREATE' 'TIME' PIC '99:99'
 DEF P_FLAG1           (B 3.0) = 0    'F1' PIC H
 DEF FIRST_SW          (X 1) = ' '
 DEF VMS_INFO          (X 13) = ' '
   DEF VMS_CODE          =VMS_INFO 001-004 X
   DEF VMS_DATE          =VMS_INFO 006-013 X
 DEF NUM_DATASETS      (X 13) = ' '
   DEF NUM_TEXT        = NUM_DATASETS 001-009 X
   DEF NUM_FILES       = NUM_DATASETS 011-013 N PIC '999'
 DEF P_COMMENT         (X 13) = ' ' 'COMMENTS'
 DEF VOL_COUNT         (N 6.0) = 0 'VOLUMES'
 DEF DSNB_COUNT        (N 6.0) = 0 'DSNBS'

!
! USING A TYPRUN OF R_SEQUENTIAL WILL SELECT VOLUME AND DSNB RECORDS
! USING A TYPRUN OF R_VOLUME WILL SELECT ONLY VOLUME RECORDS
! USING A TYPRUN OF R_CHAINED WILL SELECT BASE VOLUME, THEN DSNB'S
!

!SET TYPRUN = R_SEQUENTIAL
 SET TYPRUN = R_VOLUME
!SET TYPRUN = R_CHAINED

 GOTO EOJ TAPEDB = 'E'
 GET TAPEDB
 GOTO TEST TAPEDB = 'E'

 IF FILESEQ = 0                   ! Q. UNALLOCATED DSNB RECORD
     GOTO START                   ! A. EQUAL - YES, BYPASS RECORD
 ENDIF

 IF ACTIND = 'N' AND FILESEQ > 1  ! Q. UNALLOCATED DSNB RECORD
     GOTO START                   ! A. EQUAL - YES, BYPASS RECORD
 ENDIF

 IF FLAG1_DEL = 'Y'               ! Q. TAPE IN DELETED STATUS
     GOTO START                   ! A. EQUAL - YES, BYPASS RECORD
 ENDIF

!IF CJOB = 'PRE-TMS'              ! Q. TAPE IN "PRE" STATUS
!    GOTO START                   ! A. EQUAL - YES, BYPASS RECORD
!ENDIF

!IF SCRATCH_IND = 'Y'             ! Q. TAPE IN SCRATCH STATUS
!    GOTO START                   ! A. EQUAL - YES, BYPASS RECORD
!ENDIF

 CONTINUE:
 IF FIRST_SW = ' '
    THEN SET FIRST_SW = '*'
    SET FUNCTION = 'RETURN_DATE'
    CALL CTSEDATE USING FUNCTION,RUNDATE,RUNTIME
    SET FUNCTION = 'TO_EXPLODED'
    CALL CTSEDATE USING FUNCTION,P_EXP_DATA,RUNDATE
 ENDIF

 SET FUNCTION = 'TO_EXTERNAL'
 CALL  CTSEDATE  USING FUNCTION,P_EXPDT,EXPDT,DATE_FMT
 CALL  CTSEDATE  USING FUNCTION,P_CDATE,CDATE DATE_FMT
 CALL  CTSEDATE  USING FUNCTION,P_LDATE,LDATE,DATE_FMT

 SET D_CTIME  = CTIME / 100
 SET D_LBLTYPE = LABEL_TYPE

 IF FILESEQ > 1 THEN
     SET P_FLAG1  = DSNBFLG1
     SET D_VOLSER = '      '
     SET D_LBLTYPE = 'DSNB'
     SET DSNB_COUNT = DSNB_COUNT + 1
   ELSE
     SET P_FLAG1  = VOLFLG1
     SET D_VOLSER = VOLSER
     SET VOL_COUNT = VOL_COUNT + 1
 ENDIF

 IF OUTCODE = '    ' OR OUTCODE = ' VMS' OR OUTCODE < '    '
     SET P_COMMENT = '             '
   ELSE
     SET VMS_CODE = OUTCODE
     CALL CTSEDATE USING FUNCTION,VMS_DATE,OUTDATE,DATE_FMT
     SET P_COMMENT = VMS_INFO
 ENDIF

 IF  FILESEQ = 1 THEN
     SET P_COMMENT = '*PRIVATE*    '
 ENDIF

 IF SCRATCH_IND = 'Y' THEN
     SET P_COMMENT = '*SCRATCH*    '
 ENDIF

 IF FLAG3_BAD = 'Y' THEN
     SET P_COMMENT = '*BAD*        '
 ENDIF

 REPORT TITLE  @1 P_EXP_DAYNAM @11 ',' @13 P_EXP_MONNAM @24 P_EXP_DD
               @26 ',' @28 P_EXP_YYYY @32 '.' @33 P_EXP_DDD
               @50 COMPANY_NAME @115 'PAGE' @125 PAGENUMBER
        TITLE  @1 ' '
        TITLE  @1 'TMS REPORT-TMLIBTM  VOLUME SERIAL MASTER'

 SELECT TAPEDB NOT = 'E'

 CONTROL VOLSER VOLSEQ FILESEQ

 PRINT D_VOLSER 1 DSN 1 CJOB        VOLSEQ  1 P_CDATE D_CTIME 1
     P_EXPDT 1 ROBOT_TYPE ROBOT_NUMBER @112 D_LBLTYPE @117 P_COMMENT

 REPORT TITLE  @1 P_EXP_DAYNAM @11 ',' @13 P_EXP_MONNAM @24 P_EXP_DD
               @26 ',' @28 P_EXP_YYYY @32 '.' @33 P_EXP_DDD
               @50 COMPANY_NAME @115 'PAGE' @125 PAGENUMBER
        TITLE  @1 ' '
        TITLE  @1 'TMS REPORT-TMLIBTM  TOTALS FOR VOLUME SERIAL MASTER'
 SELECT TAPEDB = 'E'
 PRINT 0 'TOTAL NUMBER OF ITEMS LISTED:' VOL_COUNT 5 DSNB_COUNT
END
