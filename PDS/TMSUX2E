TMSUX2E  TITLE 'CA-1  TAPE MANAGEMENT SYSTEM  -  MODULE (TMSUX2E)'
*****************************************************************
*    TMSUX2E - CONVERT CA-1  INTERNAL VOLSERS TO INSTALLATION
*              STANDARD (ALPHANUMERIC) VOLSERS
*
*    REGISTER USAGE
*        R0    UNUSED
*        R1    POINTS TO A POINTER TO A VOLSER & 10 BYTE WORKAREA
*        R2    UNUSED
*        R3    BASE REGISTER
*        R4    UNSED
*        R5    UNUSED
*        R6    UNUSED
*        R7    UNUSED
*        R8    UNUSED
*        R9    UNUSED
*        R10   UNUSED
*        R11   UNUSED
*        R12   UNUSED
*        R13   CALLER'S SAVE AREA
*        R14   RETURN ADDRESS
*        R15   RETURN CODE (NOT REQUIRED)
*
*    SPECIFICATIONS
*        AMODE ANY
*        RMODE 24
*
*    EXIT
*        VOLSER POINTED TO BY PARAMETER LIST MUST CONTAIN AN
*        ALPHANUMERIC VOLSER DEFINED IN THE TMC.
*
*****************************************************************
         EJECT
TMSUX2E  CSECT ,
TMSUX2E  AMODE ANY
TMSUX2E  RMODE 24
         TMMLINK (R3),NAME=TMSUX2E,SAVAREA=NO,EQUS=YES,                X
               SSI=CL052B0
         EJECT
*---------------------------------------------------------------------*
*    CHANGES:                                                         *
*        PARMLIB: EXTERNALIZE USER EXITS                              *
*        PROCESS PARAMETER LIST CORRECTLY                             *
*        ADD THE TAG RETURN FOR EXITING FROM TMSUX2E                  *
*     SUPPORT FOR > 999,999 INTERNAL VOLSER'S                         *
*                                                                     *
*  COMMENT "CLI 0(R1),0"                                        -RR1  *
*  COMMENT "NEWSTYLE" BRANCH AND LABEL                          -RR1  *
*  COMMENT "B RETURN" BRANCH                                    -RR1  *
*  ADD  "TR 0(1,1),INT2EXT"                                     +RR1  *
*  COPY XLATE TABLE CODE: !HS1                                  +RR1  *
*  ADD V AND DV TAPE TYPES                                      +SBG  *
*---------------------------------------------------------------------*
         L     R1,0(,R1)          GET ADDRESS OF VOLSER
*        CLI   0(R1),0            IS IT NEW STYLE ?                -RR1
*        BE    NEWSTYLE           YES THEN BRANCH                  -RR1
*
*
         CLC   0(2,R1),=C'10'     NEW DEVELOPMENT VOLUMES?          SBG
         BNE   DOTRAN             NO. REGULAR PROCESSING            SBG
         MVC   0(2,R1),=C'DV'     YES. MOVE IN DV INSTEAD           SBG
         B     RETURN             AND GET OUT                       SBG
DOTRAN   DS    0H                                                   SBG
         TR    0(1,1),INT2EXT     XLATE INTERNAL (CA-1) PRESENT-   +RR1
*                                 ATION TO EXTERNAL (IBM) VOLSER   +RR1
*                                 OLD TABLE INCLUDED BELOW W/ !HS1.+RR1
*
*        B     RETURN                                              -RR1
NEWSTYLE DS    0H                                                  -RR1
*
*        PLACE NEWSTYLE USER CODE HERE
*
RETURN   DS    0H
         LA    R15,0              SET RC TO ZERO
*
         RETURN (14,12),RC=(15)
         EJECT
*                                                                  !HS1
* THE PURPOSE OF THIS ROUTINE IS TO CONVERT INTERNAL VOLUME SERIAL !HS1
* NUMBERS TO EXTERNAL FORMAT. CA-1  REQUIRES ALL NUMERIC           !HS1
* VOLSERS  FOR ITS PROCESSING. SINCE A & P USES THE FIRST CHARATER !HS1
* OF OUR VOL SERS TO INDICATE THE "OWNER" OF THE TAPE, A NUMERIC   !HS1
* TO ALPHA  MAPPING IS REQUIRED.                                   !HS1
*                                                                  !HS1
*------------------------------------------------------------------!HS1
*   ANP TABLE FOR TRANSLATION FROM INTERNAL (CA1) REPRESENTATION   !HS1
*   TO EXTERNAL (IBM) VOLSER                                       !HS1
*------------------------------------------------------------------!HS1
INT2EXT  DS    0D                                                  !HS1
         DC    256AL1(*-INT2EXT) TABLE THAT TRANSLATES TO ITSELF   !HS1
*                                                                  !HS1
         ORG   INT2EXT+C'0'     0 -> D FOR DEVELOPMENT             !HS1
         DC    C'D'                                                !HS1
*        ORG   INT2EXT+C'1'     10-> DV FOR DEVELOPMENT VIRTUAL
*        DC    C'DV'            THIS IS FOR DOC (2 CHARS NG FOR TRT)
         ORG   INT2EXT+C'2'     2 -> C FOR COLUMBIA                !HS1
         DC    C'C'                                                !HS1
         ORG   INT2EXT+C'4'     4 -> M FOR MONTVALE                !HS1
         DC    C'M'                                                !HS1
         ORG   INT2EXT+C'6'     6 -> T FOR TORONTO                 !HS1
         DC    C'T'                                                !HS1
         ORG   INT2EXT+C'8'     8 -> V FOR VIRTUAL TAPES            SBG
         DC    C'V'                                                 SBG
         ORG   INT2EXT+C'9'     9 -> W FOR TECH SERVICES            SBG
         DC    C'W'                                                 SBG
         LTORG ,
         TMMLINK PATCH
         END
