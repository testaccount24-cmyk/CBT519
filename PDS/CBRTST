*********************************************************************
*                                                                   *
*       CBRTEST    -   TEST CTSQSTS RETURN CODES FOR A VOLSER       *
*       -------        --------------------------------------       *
*                                                                   *
*    AUTHOR:   SAM GOLOB                DATE:  MAY 25, 01           *
*                                                                   *
*    SAMPLE EXECUTION JCL:                                          *
*                                                                   *
*       //WSBGCBRT JOB ,'SAM GOLOB',CLASS=S,                        *
*       //    MSGCLASS=X,NOTIFY=&SYSUID                             *
*       //*                                                         *
*       //TESTCALL EXEC PGM=CBRTST                                  *
*       //STEPLIB  DD   DISP=SHR,DSN=WSBG.LOAD                      *
*       //SYSPRINT DD   SYSOUT=*,DCB=RECFM=FBA                      *
*       //SYSIN    DD   DISP=SHR,DSN=WSBG.EDIT.SPLIT.STATEMTS       *
*                                                                   *
*     OR SYSIN MIGHT LOOK LIKE THIS:                                *
*                                                                   *
*       //SYSIN    DD   *                                           *
*       M10000                                                      *
*       M10001                                                      *
*       M10002                                                      *
*       M10003                                                      *
*       M10004                                                      *
*       M10005                                                      *
*       M10006                                                      *
*       M10007                                                      *
*       M10008                                                      *
*       M10009                                                      *
*       M10010                                                      *
*       /*                                                          *
*                                                                   *
*                                                                   *
*         THE SYSIN DATASET HAS TO BE FB-80 AND CONTAIN A           *
*         VOLUME SERIAL IN COLUMNS 1-6.  ASTERISK '*' OR BLANK      *
*         IN COLUMN1 IS REGARDED AS A COMMENT.                      *
*                                                                   *
*         YOU CAN MODIFY THE CODE AT LABEL "COMMENT" TO FILTER      *
*         THE SYSIN INPUT THE WAY YOU WANT IT.                      *
*                                                                   *
*         I RAN THIS PROGRAM AGAINST ALL THE VOLUMES IN MY TMC.     *
*         I COPIED THE TMC RECORDS INTO AN FB-80 DATASET,           *
*         TRUNCATING THE ENDS.  THEN I EDITED OUT THE DSNB          *
*         RECORDS, LEAVING RECORDS CONTAINING ONLY VOLUME           *
*         SERIAL NUMBERS IN COLUMNS 1-6.  THAT FILE IS INPUT        *
*         TO THE CBRTST PROGRAM.                                    *
*                                                                   *
*         IT IS ALSO EASY TO MODIFY THE RETURN CODE INTERPRETATION  *
*         MESSAGES AT LABEL CODE0, ETC.                             *
*                                                                   *
*         AND LAST BUT NOT LEAST, YOU CAN CALL A DIFFERENT          *
*         PROGRAM, IF YOU HAVE NEED TO.  JUST LOOK AT THE           *
*         PROGRAM NAME LITERAL BEFORE THE LOAD MACRO.  AND          *
*         YOU ALSO HAVE TO DO THE SAME THING AT THE DELLOAD LABEL.  *
*         THE "DELETE" MACRO WILL ALSO PROBABLY HAVE TO BE REMOVED  *
*         OR MODIFIED, IN THE DELLOAD SUBROUTINE.                   *
*                                                                   *
*         OF COURSE, THE INPUT FILE MAY HAVE TO BE HANDLED          *
*         DIFFERENTLY TOO.  CURRENTLY IT IS A LIST OF VOLSERS.      *
*         AND THE OTHER PROGRAM MAY HAVE A LONGER PARM LIST.        *
*         BUT THIS PROGRAM IS STILL QUITE GENERAL.                  *
*                                                                   *
*********************************************************************
*                                                                   *
*  CHANGE LOG:                                                      *
*                                                                   *
*    05/25/01 - RUNNING THIS PROGRAM AGAINST THE OLDER VERSION OF   *
*    (S.GOLOB)  CTSQSTS, WHICH DID A LOAD OF TMSSVC WITHOUT A       *
*               DELETE, CAUSED A S906-08 ABEND AFTER 32767 INPUT    *
*               VOLUME RECORDS WERE READ.  TMSSVC USE COUNT WAS     *
*               RAISED TO 32767, AND COULD BE INCREASED NO MORE.    *
*                                                                   *
*               THIS PROGRAM NOW DOES A DELETE OF TMSSVC, EACH TIME *
*               CTSQSTS IS CALLED, BECAUSE IT RUNS IN THE SAME TCB  *
*               AS CTSQSTS, WHEN THE LATTER PROGRAM LOADS TMSSVC.   *
*               THEREFORE OUR DELETE OF THE TMSSVC PROGRAM IS       *
*               EFFECTIVE.  RUSSELL WITT (THE AUTHOR OF CTSQSTS)    *
*               HAS TOLD ME THAT CTSQSTS (AT A LATER LEVEL) DOES    *
*               THE DELETE OF TMSSVC, ITSELF.                       *
*                                                                   *
*               WE DISCOVERED THIS PROBLEM BY RUNNING CBRTST        *
*               AGAINST ALL VOLUMES IN THE TMC.  WHEN WE ENLARGED   *
*               OUR TMC (TO GREATER THAN 32767 VOLUMES), THIS       *
*               PROBLEM "REARED ITS UGLY HEAD", AND HAD TO BE       *
*               FIXED.                                              *
*                                                                   *
*********************************************************************
         EJECT
CBRTEST  CSECT
         YREGS
         USING *,R15
         B     EYECATCH
         DC    C'--- '            MAKE EYECATCHER VISIBLE
         DC    C'CBRTEST - TEST CTSQSTS RETURN CODES - '
         DC    C'&SYSDATE',C'  ',C'&SYSTIME ',C'--- '
SAVEAREA DC    18F'0'
EYECATCH DS    0H             ENSURE ALIGNMENT
         SAVE  (14,12)
         LR    R5,R1          SAVE PARM POINTER
         LA    R1,SAVEAREA          NEW SAVE-AREA ADDR
         ST    R1,8(,R13)           FORWARD SAVE-AREA CHAIN PTR
         ST    R13,4(,R1)           BACKWARD SAVE-AREA CHAIN PTR
         LR    R13,R1               NEW SAVE AREA ADDRESS
         DROP  R15
* -----                       SET UP ONE BASE REGISTER
         USING CBRTEST,R12
         LR    R12,R15
         LR    R1,R5          RESTORE PARM POINTER
         L     R1,0(R1)       GET PARM FROM JCL
         OPEN  (SYSIN,INPUT)
         OPEN  (SYSPRINT,OUTPUT)
         ZAP   PAGECT,=P'0'
         ZAP   ITEMCT,=P'0'
         ZAP   LOADCT,=P'0'
         BAL   R4,HEADING
         LA    R0,=CL8'CTSQSTS '
         LOAD  EPLOC=(0)
         ST    R0,ENTRYPT
GETLOOP  DS    0H
         CP    LINECT,=P'60'
         BL    NOPAGE
         BAL   R4,HEADING
NOPAGE   DS    0H
         MVC   INAREA,INAREA-1    CLEAR INPUT AREA TO BLANKS
         GET   SYSIN,INAREA       GET A VOLSER RECORD
COMMENT  CLI   SYSIN,C'*'
         BE    GETLOOP
         CLI   SYSIN,C' '
         BE    GETLOOP
         MVC   VOLSR(6),INAREA    MOVE VOLSER TO INPUT AREA
         PRINT GEN
* --------  PRINT HEADER AND INITIAL VOLUME NAME MESSAGE
         MVC   OUTLINE,OUTLINE-1
         MVC   MSG01+20(6),VOLSR
* --------  CODE TO CALL CTSQSTS AND GET THE RETURN CODE
* -*-*-
         CP    LOADCT,=P'30000'   USE COUNT GETTING TOO HIGH?
         BNH   NOLOADIT           NO. BYPASS DELETE.
         BAL   R4,DELLOAD         DELETE AND RELOAD
NOLOADIT DS    0H
* -*-*-
         XC    LQUERY(16),LQUERY  CLEAR PARM AREA
         LA    R0,VOLSR           POINT TO VOLSER NAME
         ST    R0,LQUERY          PUT VOLSER INTO PARM LIST
         OI    LQUERY,X'80'       INDICATE END OF PARM LIST
         L     R15,ENTRYPT        GET ENTRY POINT OF CALLED PROGRAM
         LA    R1,LQUERY               POINT TO PARM LIST
         BALR  R14,R15                 DO CALL
         CVD   R15,DOUBLE         CONVERT TO PACKED
         ST    R15,SAVER15        SAVE RETURN CODE
         DELETE EP=TMSSVC
         AP    LOADCT,=P'1'
         OI    DOUBLE+7,X'0F'     SIGN NIBBLE
         MVC   MSG02+20(10),EDPAT
         ED    MSG02+20,DOUBLE+3
         L     R15,SAVER15        RESTORE RETURN CODE
         BAL   R4,CODETEXT        PUT INTERPRETATION INTO MESSAGE
         MVC   OUTLINE,OUTLINE-1
         MVC   OUTLINE(133),MSG01
         PUT   SYSPRINT,OUTLINE
         AP    LINECT,=P'1'
         B     GETLOOP
* --------
GETEND   DS    0H
         CLOSE SYSIN
         CLOSE SYSPRINT
THEEND   DS    0H
         L     R13,4(,R13)
         RETURN (14,12),RC=0
* --------         SUBROUTINE - PRINT HEADING
HEADING  DS    0H
         ZAP   LINECT,=P'4'
         AP    PAGECT,=P'1'
         MVC   OUTLINE,OUTLINE-1
         MVC   HEADPAT,EDPAT
         ED    HEADPAT,PAGECT+2
         MVC   OUTLINE(133),HEADER
         PUT   SYSPRINT,OUTLINE
         MVC   OUTLINE,OUTLINE-1
         MVI   OUTLINE,C'-'
         PUT   SYSPRINT,OUTLINE
         BR    R4
* --------         SUBROUTINE - CODE TEXT INTERPRETATION
CODETEXT MVC   CODETXT,BLANKS
TRYC0    C     R15,=F'0'
         BNE   TRYC4
         MVC   CODETXT,CODE0
         B     TRYCEND
TRYC4    C     R15,=F'4'
         BNE   TRYC8
         MVC   CODETXT,CODE4
         B     TRYCEND
TRYC8    C     R15,=F'8'
         BNE   TRYC12
         MVC   CODETXT,CODE8
         B     TRYCEND
TRYC12   C     R15,=F'12'
         BNE   TRYC16
         MVC   CODETXT,CODE12
         B     TRYCEND
TRYC16   C     R15,=F'16'
         BNE   TRYC20
         MVC   CODETXT,CODE16
         B     TRYCEND
TRYC20   C     R15,=F'20'
         BNE   TRYCERR
         MVC   CODETXT,CODE20
         B     TRYCEND
TRYCERR  MVC   CODETXT,CODEERR
TRYCEND  BR    R4
* --------         SUBROUTINE - DELETE AND RELOAD OF CALLED PROGRAM
DELLOAD  DS    0H
         STM   R0,R1,SAVER0R1      SAVE R0 AND R1
         ZAP   LOADCT,=P'0'        RESET COUNTER TO ZERO
         L     R0,ENTRYPT          GET ENTRY POINT
*                                  DELETE AND RELOAD
         DELETE EPLOC=(0)
         LA    R0,=CL8'CTSQSTS '
         LOAD  EPLOC=(0)
         ST    R0,ENTRYPT
         LM    R0,R1,SAVER0R1      RESTORE R0 AND R1
DELLEND  BR    R4
* ----- CONSTANTS ------ *
DOUBLE   DC    D'0'
SAVER0R1 DC    D'0'
SAVER15  DC    F'0'
ENTRYPT  DC    F'0'
PAGECT   DC    PL7'0'
LINECT   DC    PL7'0'
ITEMCT   DC    PL7'0'
LOADCT   DC    PL7'0'
EDPAT    DC    X'40202020202020202120'
         DC    C' '
BLANKS   DC    CL133' '
OUTLINE  DS    CL133
HEADER   DS    0CL133
         DC    CL103'1  CTSQSTS - RETURN CODE TEST PROGRAM     '
         DC    CL5'PAGE '
HEADPAT  DC    CL10' '
         DC    CL15' '
         DC    CL15' '
MSG01    DS    0CL133
         DC    CL31'     INPUT VOLSER =            '
MSG02    DC    CL43'     RETCODE WAS  =                       '
CODETXT  DC    CL14' '
         DC    CL45' '
         DC    CL8' '
VOLSR    DC    CL6' '
RETCDE   DC    PL7'0'
LQUERY   DS    A                       ADR OF VOLSER
         DS    A                       ADR OF ATL-TYPE, ATL-NUM
         DS    A                       ADR OF CONTAINER VOLSER
         DS    A                       ADR OF DSN FOR CONTAINER VOLUME
LQUERYE  EQU   *                       END-OF-LIST
CODETABL DS    0H
CODE0    DC    C'SCRATCH TAPE  '
CODE4    DC    C'TAPE NOT FULL '
CODE8    DC    C'FULL TAPE     '
CODE12   DC    C'OUT OF SERVICE'
CODE16   DC    C'FOREIGN TAPE  '
CODE20   DC    C'CA1 NOT ACTIVE'
CODEERR  DC    C'INVALID CODE  '
LINKPARM DS    0A                 <--- R15 AT LINK TIME
         DC    A(0)                    ADR OF PGM NAME
         DC    A(0)                    NO ADR FOR DCB
         DC    C' '
INAREA   DC    CL80' '
         LTORG
SYSPRINT DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=PM,LRECL=133
SYSIN    DCB   DDNAME=SYSIN,DSORG=PS,MACRF=GM,LRECL=80,EODAD=GETEND
         END   CBRTEST
