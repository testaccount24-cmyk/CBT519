TMLIBA04  START 0
         SPACE 2
**** START OF SPECIFICATIONS ******************************************
*                                                                     *
*    MODULE NAME:       TMLIBA04                                      *
*                                                                     *
*    PURPOSE:  TO QUERY THE VTS ON A VOLUME-BY-VOLUME BASIS, AND      *
*              RETURN THE RESULTS IN THE FORM OF A SPECIAL FORMAT     *
*              FILE, RECFM=FB,LRECL=385.  THIS FILE IS USED TO INPUT  *
*              THE VTS LIBRARY DATA INTO THE TMLIBAUD PROGRAM.        *
*                                                                     *
*                                                                     *
*    AUTHOR:   SAM GOLOB            DATE:  05/31/01                   *
*                                                                     *
*    INPUTS:   INDD     -  VOLUME LIST CREATED FROM THE TMLIBA03      *
*                          PROGRAM WHICH PRODUCES THE BOTH AND        *
*                          VNOT FILES.  THESE VOLUME NAMES WERE       *
*                          ORIGINALLY GENERATED FROM THE VOLCAT       *
*                          LISTING BY THE TMLIBA02 PROGRAM.           *
*                                                                     *
*    OUTPUTS:  OUTDD    -  OUTPUT FB-385 FILE SHOWING VOLUME DATA     *
*                          FROM BOTH THE TCDB AND THE LIBRARY.  THIS  *
*                          FILE IS IN A SPECIAL FORMAT THAT WE HAVE   *
*                          DEFINED.  USED FOR THE TMLIBAUD PROGRAM.   *
*                                                                     *
*              SYSPRINT -  PRINTS RUN STATISTICS                      *
*                                                                     *
*                                                                     *
*     CHANGE LOG:                                                     *
*                                                                     *
*     LEVEL 002                                                       *
*     06/07/01 - SYSPRINT ADDED WITH DATE AND TIME, INPUT AND         *
*                OUTPUT COUNTS.                                       *
*                                                                     *
*                                                                     *
*                                                                     *
**** END OF SPECIFICATIONS ********************************************
         TITLE 'STANDARD REGISTER DEFINITIONS'
*---------------------------------------------------------------------*
*                                                                     *
*        STANDARD REGISTER DEFINITIONS                                *
*                                                                     *
*---------------------------------------------------------------------*
R0       EQU   0                    GENERAL REGISTER 0
R1       EQU   1                    GENERAL REGISTER 1
R2       EQU   2                    GENERAL REGISTER 2
R3       EQU   3                    GENERAL REGISTER 3
R4       EQU   4                    GENERAL REGISTER 4
R5       EQU   5                    GENERAL REGISTER 5
R6       EQU   6                    GENERAL REGISTER 6
R7       EQU   7                    GENERAL REGISTER 7
R8       EQU   8                    GENERAL REGISTER 8
R9       EQU   9                    GENERAL REGISTER 9
R10      EQU   10                   GENERAL REGISTER 10
R11      EQU   11                   GENERAL REGISTER 11
R12      EQU   12                   GENERAL REGISTER 12
R13      EQU   13                   GENERAL REGISTER 13
R14      EQU   14                   GENERAL REGISTER 14
R15      EQU   15                   GENERAL REGISTER 15
         TITLE 'CBRLCSPL - LCS EXTERNAL SERVICES PARAMETER LIST'
         CBRLCSPL  ,                LCS EXTERNAL SERVICES PARM LIST
         TITLE 'TMLIBA04 - CREATE VTS VOLUME DATA FILE'
*---------------------------------------------------------------------*
*                                                                     *
*        TMLIBA04 ENTRY POINT                                         *
*                                                                     *
*---------------------------------------------------------------------*
TMLIBA04  CSECT ,                   SAMPLE INSTALLATION MGMT PACKAGE
TMLIBA04  AMODE 24
TMLIBA04  RMODE 24
         SAVE  (14,12),,            SAVE CALLER'S REGISTERS AND        +
               'TMLIBA04 &SYSDATE &SYSTIME - LEVEL 002'
         USING TMLIBA04,R12,R11     TMLIBA04 BASE REGISTER
         LR    R12,R15              COPY ENTRY POINT ADDRESS
         LR    R11,R12              SET UP SECOND BASE
         LA    R11,4095(,R12)       FOR BREATHING ROOM
         LA    R11,1(,R11)
         ST    R13,SAVE+4           BACKWARD CHAIN SAVE AREAS
         LA    R0,SAVE              TMLIBA04 SAVE AREA ADDRESS
         ST    R0,8(,R13)           FORWARD CHAIN SAVE AREAS
         LR    R13,R0               SET TMLIBA04 SAVE AREA ADDRESS
         SPACE 2
***********************************************************************
         BAL   R9,TIMEDATE              GET START DATE AND TIME
         MVC   HEADER+95(23),HEADDATE   AND MOVE TO THE HEADER
         MVC   STMSG+12(23),HEADDATE    AND MOVE TO START TIME MSG
***********************************************************************
         OPEN  (SYSPRINT,OUTPUT)
         MVC   OUTLINE,OUTLINE-1         PRINT A HEADER LINE SO YOU
         MVC   OUTLINE(133),HEADER       DON'T FEEL THE PROGRAM
         PUT   SYSPRINT,OUTLINE          ISN'T DOING ANYTHING.
*
*                                        ZERO THE COUNTERS
         ZAP   INVOLC,=P'0'
         ZAP   OUTFILC,=P'0'
*---------------------------------------------------------------------*
*                                                                     *
*        OPEN BOTH DATA CONTROL BLOCKS                                *
*                                                                     *
*---------------------------------------------------------------------*
         OPEN  (INDCB,(INPUT),OUTDCB,(OUTPUT))  OPEN BOTH DCBS
         SPACE 2
*---------------------------------------------------------------------*
*                                                                     *
*        READ AND PROCESS THE INPUT TRANSACTION REQUEST DATASET       *
*                                                                     *
*---------------------------------------------------------------------*
CUA1000  DS    0H
         GET   INDCB,TRANSACT       READ FIRST/NEXT TRANSACTION
         AP    INVOLC,=P'1'         COUNT RECORD READ IN
         EJECT ,
*---------------------------------------------------------------------*
*                                                                     *
*        VERIFY THAT THE VOLUME RESIDES IN A LIBRARY, IF REQUESTED    *
*                                                                     *
*---------------------------------------------------------------------*
         BAL   R9,CUACOPY           COPY MODEL TO LCS PARAMETER LIST
         LA    R3,LCSLIST
         USING LCSPL,R3
         CBRXLCS  TYPE=TAPE,        QUERY VOLUME RESIDENCE CALL        +
               FUNC=QVR,                                               +
               VOLUME=TRANVOL,                                         +
               VOLINFO=YES,                                            +
               MF=(E,LCSLIST),                                         +
               SUBPOOL=0
         LTR   R15,R15              VOLUME IN LIBRARY?
*        BNE   CUA1000A
         BAL   R9,CUACODES          FORMAT RETURN AND REASON CODES
         BAL   R9,CUAOUTIT
CUA1000A DS    0H
         C     R15,=A(LCSWARN)      WARNING RETURN CODE?
         BNE   CUA1100              NO. FORMAT ERROR MESSAGE
         C     R0,=A(LCSFNLRS)      VOLUME NOT LIBRARY RESIDENT?
         BNE   CUA1100              NO. FORMAT ERROR MESSAGE
         BAL   R9,CUAFREE           WRITE TRANSACTION RESPONSE
         B     CUA1000              GET NEXT TRANSACTION
CUA1100  DS    0H
         BAL   R9,CUAFREE           WRITE TRANSACTION RESPONSE
         B     CUA1000              GET NEXT TRANSACTION
         EJECT ,
*---------------------------------------------------------------------*
*                                                                     *
*        CLEAN UP AND RETURN TO THE CALLER                            *
*                                                                     *
*---------------------------------------------------------------------*
EXIT     DS    0H
         BAL   R9,TIMEDATE              GET ENDING DATE AND TIME
         MVC   ENDMSG+12(23),HEADDATE    AND MOVE TO START TIME MSG
*                                 PRINT SUMMARY STATS BEFORE CLOSING
         MVC   OUTLINE,OUTLINE-1
         MVC   OUTLINE(133),MSGINV
         MVC   OUTLINE+35(12),EDPAT
         ED    OUTLINE+35(12),INVOLC+2
         MVC   OUTLINE+59(36),STMSG      ADD START TIME MESSAGE
         PUT   SYSPRINT,OUTLINE
         MVC   OUTLINE,OUTLINE-1
         MVC   OUTLINE(133),MSGOUTF
         MVC   OUTLINE+35(12),EDPAT
         ED    OUTLINE+35(12),OUTFILC+2
         MVC   OUTLINE+59(36),ENDMSG     ADD ENDING TIME MESSAGE
         PUT   SYSPRINT,OUTLINE
         CLOSE (INDCB,,OUTDCB)      CLOSE BOTH DATA CONTROL BLOCKS
         L     R13,SAVE+4           RESTORE CALLER'S SAVE AREA ADDRESS
         RETURN  (14,12),           RESTORE CALLER'S REGISTERS, THEN   +
               RC=0                   RETURN TO CALLER
         EJECT ,
*---------------------------------------------------------------------*
*                                                                     *
*        COPY THE MODEL LCS PARAMETER LIST TO THE ACTUAL LIST         *
*                                                                     *
*---------------------------------------------------------------------*
CUACOPY  DS    0H
         LA    R0,LCSMODEL          ADDRESS OF SOURCE
         LA    R1,LCSPLENG          LENGTH OF SOURCE
         LA    R14,LCSLIST          ADDRESS OF TARGET
         LR    R15,R1               LENGTH OF TARGET
         MVCL  R14,R0               COPY MODEL TO LCS PARAMETER LIST
         BR    R9                   RETURN TO CALLER
         EJECT ,
***********************************************************************
* GET SYSTEM DATE AND TIME                                            *
***********************************************************************
TIMEDATE DS    0H
         TIME  DEC,DATETIME,LINKAGE=SYSTEM,DATETYPE=MMDDYYYY
         OI    DATE+4,X'0F'
         UNPK  DATED,DATE
         OI    TIME+3,X'0F'
         UNPK  TIMED,TIME
         MVC   HEADDATE(2),DATED
         MVI   HEADDATE+2,C'/'
         MVC   HEADDATE+3(2),DATED+2
         MVI   HEADDATE+5,C'/'
         MVC   HEADDATE+6(4),DATED+4
         MVC   HEADDATE+12(2),TIMED
         MVI   HEADDATE+14,C':'
         MVC   HEADDATE+15(2),TIMED+2
         MVI   HEADDATE+17,C':'
         MVC   HEADDATE+18(2),TIMED+4
         BR    R9
         EJECT ,
*---------------------------------------------------------------------*
*                                                                     *
*        FORMAT THE RETURN AND REASON CODES FOR PRINTING              *
*                                                                     *
*---------------------------------------------------------------------*
CUACODES DS    0H
         CVD   R15,PRETCODE         CONVERT TO PACKED DECIMAL
         UNPK  ZRETCODE,PRETCODE    CONVERT TO ZONED DECIMAL
         OI    ZRETCODE+3,X'F0'     CORRECT FINAL ZONE
         CVD   R0,PRSNCODE          CONVERT TO PACKED DECIMAL
         UNPK  ZRSNCODE,PRSNCODE    CONVERT TO ZONED DECIMAL
         OI    ZRSNCODE+3,X'F0'     CORRECT FINAL ZONE
*        MVC   TRANMSG,RETREAS      MOVE TEXT TO RESPONSE AREA
         BR    R9                   RETURN TO CALLER
         EJECT ,
*
CUAOUTIT DS    0H                   CREATE OUTPUT FILE RECORDS
*                                   FROM ALL AVAILABLE DATA
*
         MVC   TVIOVOL(6),TRANVOL   FIRST GET STUFF YOU KNOW...
         MVC   TVIOLIB(8),LCSLIBNM
         MVC   TVIOCONS(8),LCSCONSN
         MVC   ERRFUNC(3),=C'QVR'   INDICATE QVR REQUEST
         L     R7,LCSTVI@           THEN GET STUFF FROM THE INQUIRY
         MVC   TVIOAREA(256),0(R7)
         MVC   TVIOAREA+256(TVILNGTH-256),256(R7)
         PUT   OUTDCB,TVIOUT
         AP    OUTFILC,=P'1'        COUNT FILE RECORDS PUT OUT
         BR    R9                   RETURN TO CALLER
         EJECT ,
*---------------------------------------------------------------------*
*                                                                     *
*        FREE THE STORAGE OBTAINED BY THE CBRXLCS CALL                *
*                                                                     *
*---------------------------------------------------------------------*
*                             WATCHING THIS JOB WITH A MONITOR,
*                             SHOWED THAT THE CBRXLCS CALL OBTAINED
*                             STORAGE IN SUBPOOL 4, AND DIDN'T
*                             FREE IT.
CUAFREE  DS    0H
         FREEMAIN RC,SP=4
         BR    R9                   RETURN TO CALLER
         TITLE 'CONSTANTS AND WORK AREAS'
*---------------------------------------------------------------------*
*                                                                     *
*        CONSTANTS AND WORK AREAS                                     *
*                                                                     *
*---------------------------------------------------------------------*
         LTORG ,                    LITERAL CONSTANTS
         EJECT ,
*---------------------------------------------------------------------*
*                                                                     *
*        DATA CONTROL BLOCKS                                          *
*                                                                     *
*---------------------------------------------------------------------*
INDCB    DCB   DDNAME=INDD,         INPUT:  TRANSACTION REQUESTS       +
               DSORG=PS,                                               +
               MACRF=GM,                                               +
               EODAD=EXIT
         EJECT ,
OUTDCB   DCB   DDNAME=OUTDD,        OUTPUT:  RESULT NOTIFICATION       +
               MACRF=PM,                                               +
               DSORG=PS,                                               +
               RECFM=FB,                                               +
               LRECL=385
         EJECT ,
SYSPRINT DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=PM,LRECL=133,RECFM=FBA
         EJECT ,
*---------------------------------------------------------------------*
*                                                                     *
*        TMLIBA04 TRANSACTION RECORD AND RESPONSE AREA                *
*                                                                     *
*---------------------------------------------------------------------*
TRANLEN  DC    AL2(L'TRANSACT)      LENGTH FOR WTO TEXT
TRANSACT DS    0CL80                TRANSACTION RECORD
TRANVOL  DS    CL6                  VOLUME SERIAL NUMBER
         DS    CL74                 SEPARATOR
         EJECT ,
*---------------------------------------------------------------------*
*                                                                     *
*        CBRXLCS PARAMETER LISTS                                      *
*                                                                     *
*---------------------------------------------------------------------*
         CBRXLCS  MF=(L,LCSLIST)    LCS EXTERNAL SERVICES PARM LIST
         SPACE 2
         CBRXLCS  MF=(L,LCSMODEL)   LCS EXTERNAL SERVICES MODEL LIST
         EJECT ,
*---------------------------------------------------------------------*
*                                                                     *
*        MISCELLANEOUS WORK AREAS                                     *
*                                                                     *
*---------------------------------------------------------------------*
SAVE     DC    18F'0'               STANDARD SAVE AREA
         SPACE 2
*---------------------------------------------------------------------*
TVIOUT   DS    0CL385
PRETCODE DC    D'0'                 CBRXLCS RETURN CODE - PACKED DEC
PRSNCODE DC    D'0'                 CBRXLCS REASON CODE - PACKED DEC
TVIOVOL  DC    CL6' '
TVIOLIB  DC    CL8' '
TVIOCONS DC    CL8' '
TVIOAREA DC    (TVILNGTH)X'00'      SHOULD BE 288 CHARACTERS
         SPACE 2
RETREAS  DS    0CL45
ERRFUNC  DC    CL5' '               'QVR', 'CUA', OR 'EJECT'
         DC    CL15' RETURN CODE = '
ZRETCODE DC    CL4' '               CBRXLCS RETURN CODE - ZONED DEC
         DC    CL16', REASON CODE = '
ZRSNCODE DC    CL4' '               CBRXLCS REASON CODE - ZONED DEC
         DC    CL1'.'
         DC    CL14' '            FILL OUT 385 CHARACTER OUTPUT
*---------------------------------------------------------------------*
         DC    CL20' '      SPACER
LIBLVOL  DC    CL6' '       LAST VOLUME NAME READ FROM VOL LIST
EDPAT    DC    X'402020206B2020206B202120'
         DC    C' '
BLANKS   DC    CL133' '
OUTLINE  DS    CL133
HEADER   DC    CL133'1  TMLIBA04 - GET LIBRARY MANAGER AND VOLCAT DATA -
               FOR TAPE VOLUMES'
MSGINV   DC    CL133'-            VOLUME RECORDS  IN ='
         DC    CL133' '
MSGOUTF  DC    CL133'        OUTPUT FILE RECORDS OUT ='
         DC    CL133' '
INVOLC   DC    PL7'0'
OUTFILC  DC    PL7'0'
         SPACE 2
         DC    CL8' '
TIMED    DS    CL7
DATED    DS    CL9
DATETIME DS    4F
         ORG   DATETIME
TIME     DS    CL4
         DS    CL4
DATE     DS    CL5
         DS    CL3
HEADDATE DC    CL23' '
STMSG    DC    CL36'START TIME:                       '
ENDMSG   DC    CL36'  END TIME:                       '
ENDDATA  EQU   *
         SPACE 2
         CBRTVI
         END   TMLIBA04
