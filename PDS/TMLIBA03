*********************************************************************
*                                                                   *
*  TMLIBA03 - PROGRAM TO MATCH-MERGE LISTS OF VOLUMES CREATED BY    *
*             THE TMLIBA01 AND TMLIBA02 PROGRAMS TO PRODUCE THREE   *
*             OUTPUTS, REFLECTING VOLUMES IN ONE LIST, BUT NOT      *
*             THE OTHER, AND VOLUMES THAT ARE IN BOTH LISTS.        *
*                                                                   *
*             BOTH INPUT LISTS ARE EXPECTED TO BE SORTED ASCENDING  *
*             ON THE VOLUME IN COLS 1-6.                            *
*                                                                   *
*                                                                   *
*   AUTHOR:   SAM GOLOB            DATE:  05/31/01                  *
*                                                                   *
*   INPUTS:   TVLIN    -  VOLUME LIST CREATED FROM THE TMC          *
*             VVLIN    -  VOLUME LIST CREATED FROM THE TCDB         *
*                                                                   *
*   OUTPUTS:  TNOV     -  OUTPUT FB-80, VOLUMES IN TMC & NOT TCDB   *
*             VNOT     -  OUTPUT FB-80, VOLUMES IN TCDB & NOT TMC   *
*             BOTH     -  OUTPUT FB-80, VOLUMES IN BOTH PLACES      *
*             SYSPRINT -  PRINTS RUN STATISTICS                     *
*                                                                   *
*                                                                   *
*   CHANGE LOG:                                                     *
*                                                                   *
*                                                                   *
*                                                                   *
*                                                                   *
*                                                                   *
*********************************************************************
TMLIBA03 CSECT
         YREGS
         USING *,R15
         B     EYECATCH
         DC    C'--- '            MAKE EYECATCHER VISIBLE
         DC    C'TMLIBA03 - MERGE TMC VOLLIST WITH VOLCAT VOLLIST - '
         DC    C'&SYSDATE',C'  ',C'&SYSTIME ',C'--- '
         DC    C'LEVEL 001 '
         DC    C'--- '            MAKE EYECATCHER VISIBLE
SAVEAREA DC    18F'0'
EYECATCH DS    0H             ENSURE ALIGNMENT
         SAVE  (14,12)
         LR    R5,R1          SAVE PARM POINTER
         LA    R1,SAVEAREA          NEW SAVE-AREA ADDR
         ST    R1,8(,R13)           FORWARD SAVE-AREA CHAIN PTR
         ST    R13,4(,R1)           BACKWARD SAVE-AREA CHAIN PTR
         LR    R13,R1               NEW SAVE AREA ADDRESS
         DROP  R15
* -----                       SET UP TWO BASE REGISTERS
         USING TMLIBA03,R12,R11
         LR    R12,R15
         LR    R11,R12
         LA    R11,4095(,R12)
         LA    R11,1(,R11)
         LR    R1,R5          RESTORE PARM POINTER
         PRINT GEN
***********************************************************************
* GET SYSTEM DATE AND TIME                                            *
***********************************************************************
         TIME  DEC,DATETIME,LINKAGE=SYSTEM,DATETYPE=MMDDYYYY
         OI    DATE+4,X'0F'
         UNPK  DATED,DATE
         OI    TIME+3,X'0F'
         UNPK  TIMED,TIME
         MVC   HEADDATE(2),DATED
         MVI   HEADDATE+2,C'/'
         MVC   HEADDATE+3(2),DATED+2
         MVI   HEADDATE+5,C'/'
         MVC   HEADDATE+6(4),DATED+4
         MVC   HEADDATE+12(2),TIMED
         MVI   HEADDATE+14,C':'
         MVC   HEADDATE+15(2),TIMED+2
         MVI   HEADDATE+17,C':'
         MVC   HEADDATE+18(2),TIMED+4
         MVC   HEADER+95(23),HEADDATE
***********************************************************************
PROCESS  DS    0H                        BEGIN MATCH-MERGE
*                                        OPEN INPUTS AND PRINT
         OPEN  (TVLIN,(INPUT))
         OPEN  (VVLIN,(INPUT))
         OPEN  (SYSPRINT,OUTPUT)
         OPEN  (TNOV,OUTPUT)
         OPEN  (VNOT,OUTPUT)
         OPEN  (BOTH,OUTPUT)
         MVC   OUTLINE,OUTLINE-1         PRINT A HEADER LINE SO YOU
         MVC   OUTLINE(133),HEADER       DON'T FEEL THE PROGRAM
         PUT   SYSPRINT,OUTLINE          ISN'T DOING ANYTHING.
*
*                                        ZERO THE COUNTERS
         ZAP   INTMCC,=P'0'
         ZAP   INLIBC,=P'0'
         ZAP   OUTTNV,=P'0'
         ZAP   OUTVNT,=P'0'
         ZAP   OUTBOTH,=P'0'
         MVI   FLAG,X'00'                AND THE FLAG
         MVC   TMCLVOL,BLANKS            AND THE 'LAST VOLUME READ'
         MVC   LIBLVOL,BLANKS             STORAGE AREAS
         MVC   AREAIN,AREAIN-1           AND BLANK THE
         MVC   AREAJN,AREAJN-1            INPUT AREAS
*
INITREAD DS    0H                        INITIALIZATION OF FILES
         GET   TVLIN,AREAIN
*                                        IF EOF IT'LL GO TO FTN
         OI    FLAG,X'01'                INDICATE TMC RECD GOTTEN
         AP    INTMCC,=P'1'              INCREMENT INPUT COUNTER
         GET   VVLIN,AREAJN
*                                        IF EOF IT'LL GO TO FVN
         OI    FLAG,X'02'                INDICATE LIB RECD GOTTEN
         AP    INLIBC,=P'1'              INCREMENT INPUT COUNTER
         MVC   TMCLVOL,AREAIVOL          MOVE VOLSER NAMES --
         MVC   LIBLVOL,AREAJVOL           - BLANK IF NO INPUT
*
MATMERGE DS    0H                        MATCH MERGE PROCESS
         TM    FLAG,X'30'                EOF ON BOTH FILES?
         BO    EOFS                      YES. FINISH UP.
         TM    FLAG,X'10'                EOF ON TMC VOL FILE?
         BO    LOWLIB                    YES. JUST READ FROM VOL FILE
         TM    FLAG,X'20'                EOF ON LIB VOL FILE?
         BO    LOWTMC                    YES. JUST READ FROM TMC FILE
         CLC   TMCLVOL,LIBLVOL           COMPARE WHICH VOL IS LOWER
         BH    LOWLIB                    PROCESS LIBRARY VOL LOWER
         BL    LOWTMC                    PROCESS TMC VOL LOWER
         B     INEQUAL                   PROCESS EQUAL
*
*------------------------------------------------------------------*
INEQUAL  DS    0H                        BOTH ARE EQUAL - ONE OUTPUT
         MVC   AREAOUT,AREAOUT-1         CLEAR OUTPUT AREA
         MVC   AREAOVOL(6),TMCLVOL        MOVE VOLUME SERIAL
         MVC   AREAOUT+30(VBOTHDL),VBOTHD     INDICATOR IN OUTPUT
         PUT   BOTH,AREAOUT                   WRITE ONE OUTPUT RECORD
         AP    OUTBOTH,=P'1'              INCREMENT COUNTER
         NI    FLAG,X'FF'-X'03'          TURN OF "LAST INPUT" FLAGS
* - - - - -        IF RECORDS EQUAL GET TWO NEW RECORDS      - - - *
         GET   TVLIN,AREAIN
*                                        IF EOF, GO TO FTN
         OI    FLAG,X'01'                INDICATE TMC RECD GOTTEN
         AP    INTMCC,=P'1'              INCREMENT INPUT COUNTER
         MVC   TMCLVOL,AREAIVOL          MOVE TMC VOLSER TO AREA
* - - - - -
         GET   VVLIN,AREAJN
*                                        IF EOF, GO TO FVN
         OI    FLAG,X'02'                INDICATE LIB RECD GOTTEN
         AP    INLIBC,=P'1'              INCREMENT INPUT COUNTER
         MVC   LIBLVOL,AREAJVOL          MOVE LIB VOLSER TO AREA
* - - - - -
         B     MATMERGE                  GO LOOP
*------------------------------------------------------------------*
         SPACE 3
*
LOWTMC   DS    0H
         NI    FLAG,X'FF'-X'02'          TURN OFF LIB RECD AS LAST IN
         MVC   AREAOUT,AREAOUT-1         CLEAR OUTPUT AREA
         MVC   AREAOVOL(6),TMCLVOL        MOVE VOLUME SERIAL
         MVC   AREAOUT+30(TNOTVDL),TNOTVD     INDICATOR IN OUTPUT
         PUT   TNOV,AREAOUT
         AP    OUTTNV,=P'1'               INCREMENT COUNTER
         NI    FLAG,X'FF'-X'01'          TURN OF "LAST INPUT" FLAG
* - - - - -        GET ANOTHER TMC VOLUME RECORD             - - - *
         GET   TVLIN,AREAIN
*                                        IF EOF, GO TO FTN
         OI    FLAG,X'01'                INDICATE TMC RECD GOTTEN
         AP    INTMCC,=P'1'              INCREMENT INPUT COUNTER
         MVC   TMCLVOL,AREAIVOL          MOVE TMC VOLSER TO AREA
* - - - - -
         B     MATMERGE                  GO LOOP
*------------------------------------------------------------------*
         SPACE 3
LOWLIB   DS    0H
         NI    FLAG,X'FF'-X'01'          TURN OFF TMC RECD AS LAST IN
         MVC   AREAOUT,AREAOUT-1         CLEAR OUTPUT AREA
         MVC   AREAOVOL(6),LIBLVOL        MOVE VOLUME SERIAL
         MVC   AREAOUT+30(VNOTTDL),VNOTTD    INDICATOR IN OUTPUT
         PUT   VNOT,AREAOUT
         AP    OUTVNT,=P'1'               INCREMENT COUNTER
         NI    FLAG,X'FF'-X'02'          TURN OF "LAST INPUT" FLAG
* - - - - -        GET ANOTHER LIB VOLUME RECORD
         GET   VVLIN,AREAJN
*                                        IF EOF, GO TO FVN
         OI    FLAG,X'02'                INDICATE LIB RECD GOTTEN
         AP    INLIBC,=P'1'              INCREMENT INPUT COUNTER
         MVC   LIBLVOL,AREAJVOL          MOVE LIB VOLSER TO AREA
* - - - - -
         B     MATMERGE                  GO LOOP
*------------------------------------------------------------------*
         SPACE 3
FTN      DS    0H
         OI    FLAG,X'10'                 INDICATE EOF ON TMC VOL LIST
         MVC   TMCLVOL,=X'FFFFFFFFFFFF'    HIGH VALUES TO LAST NUMBER
         B     MATMERGE                   GO CHECK MAIN LOOP
FVN      DS    0H
         OI    FLAG,X'20'                 INDICATE EOF ON LIB VOL LIST
         MVC   LIBLVOL,=X'FFFFFFFFFFFF'    HIGH VALUES TO LAST NUMBER
         B     MATMERGE                   GO CHECK MAIN LOOP
*------------------------------------------------------------------*
EOFS     DS    0H
         CLOSE TVLIN
         CLOSE VVLIN
         CLOSE TNOV
         CLOSE VNOT
         CLOSE BOTH
OUTPRT   DS    0H
         MVC   OUTLINE,OUTLINE-1
         MVC   OUTLINE(133),MSGIT
         MVC   OUTLINE+34(12),EDPAT
         ED    OUTLINE+34(12),INTMCC+2
         PUT   SYSPRINT,OUTLINE
         MVC   OUTLINE,OUTLINE-1
         MVC   OUTLINE(133),MSGIV
         MVC   OUTLINE+34(12),EDPAT
         ED    OUTLINE+34(12),INLIBC+2
         PUT   SYSPRINT,OUTLINE
         MVC   OUTLINE,OUTLINE-1
         MVC   OUTLINE(133),MSGOTNV
         MVC   OUTLINE+34(12),EDPAT
         ED    OUTLINE+34(12),OUTTNV+2
         PUT   SYSPRINT,OUTLINE
         MVC   OUTLINE,OUTLINE-1
         MVC   OUTLINE(133),MSGOVNT
         MVC   OUTLINE+34(12),EDPAT
         ED    OUTLINE+34(12),OUTVNT+2
         PUT   SYSPRINT,OUTLINE
         MVC   OUTLINE,OUTLINE-1
         MVC   OUTLINE(133),MSGBOTH
         MVC   OUTLINE+34(12),EDPAT
         ED    OUTLINE+34(12),OUTBOTH+2
         PUT   SYSPRINT,OUTLINE
         CLOSE SYSPRINT
THEEND   DS    0H
         L     R13,4(,R13)
         RETURN (14,12),RC=0
* ----- CONSTANTS ------ *
ONE      DC    F'1'
FLAG     DC    X'00'
*              X'01'        INPUT IS FROM TMC VOLUME LIST
*              X'02'        INPUT IS FROM LIB VOLUME LIST
*              X'10'        END OF INPUT ON TMC LIST
*              X'20'        END OF INPUT ON LIB LIST
TMCLVOL  DC    CL6' '       LAST VOLUME NAME READ FROM TMC LIST
LIBLVOL  DC    CL6' '       LAST VOLUME NAME READ FROM VOL LIST
DECVALUE DS    CL8
EDPAT    DC    X'402020206B2020206B202120'
         DC    C' '
BLANKS   DC    CL133' '
OUTLINE  DS    CL133
HEADER   DC    CL133'1  TMLIBA03 - TMC AND LIB VOLUME LIST MERGE'
         DC    CL133' '
MSGIT    DC    CL133'-        TMC VOLUME RECORDS  IN ='
         DC    CL133' '
MSGIV    DC    CL133'         LIB VOLUME RECORDS  IN ='
         DC    CL133' '
MSGOTNV  DC    CL133'     TMC VOLUMES NOT IN LIBRARY ='
         DC    CL133' '
MSGOVNT  DC    CL133'     LIBRARY VOLUMES NOT IN TMC ='
         DC    CL133' '
MSGBOTH  DC    CL133'    VOLUMES IN BOTH TMC AND LIB ='
         DC    CL133' '
TNOTVD   DC    C'IN TMC AND NOT IN LIBRARY'
TNOTVDL  EQU   *-TNOTVD
VNOTTD   DC    C'IN LIBRARY AND NOT IN TMC'
VNOTTDL  EQU   *-VNOTTD
VBOTHD   DC    C'BOTH IN LIBRARY AND IN TMC'
VBOTHDL  EQU   *-VBOTHD
INTMCC   DC    PL7'0'
INLIBC   DC    PL7'0'
OUTTNV   DC    PL7'0'
OUTVNT   DC    PL7'0'
OUTBOTH  DC    PL7'0'
         LTORG
TVLIN    DCB   DDNAME=TVLIN,MACRF=GM,EODAD=FTN,DSORG=PS,LRECL=80
VVLIN    DCB   DDNAME=VVLIN,MACRF=GM,EODAD=FVN,DSORG=PS,LRECL=80
TNOV     DCB   DDNAME=TNOV,MACRF=PM,DSORG=PS,LRECL=80
VNOT     DCB   DDNAME=VNOT,MACRF=PM,DSORG=PS,LRECL=80
BOTH     DCB   DDNAME=BOTH,MACRF=PM,DSORG=PS,LRECL=80
SYSPRINT DCB   DDNAME=SYSPRINT,DSORG=PS,MACRF=PM,LRECL=133,RECFM=FBA
         DC    CL8' '
AREAIN   DS    0CL80             FIRST INPUT AREA
AREAIVOL DS    CL6
AREAIRST DS    CL74
         DC    CL8' '
AREAJN   DS    0CL80             SECOND INPUT AREA
AREAJVOL DS    CL6
AREAJRST DS    CL74
         DC    CL8' '
AREAOUT  DS    0CL80             OUTPUT AREA
AREAOVOL DS    CL6
         DC    CL74' '
         DC    CL8' '
TIMED    DS    CL7
DATED    DS    CL9
DATETIME DS    4F
         ORG   DATETIME
TIME     DS    CL4
         DS    CL4
DATE     DS    CL5
         DS    CL3
HEADDATE DC    CL23' '
         END   TMLIBA03
